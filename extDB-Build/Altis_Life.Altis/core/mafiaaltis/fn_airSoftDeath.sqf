#include <macro.h>
/*
	File: fn_airSoftDeath.sqf
	Author: Telo

	Description: Funcion de muerte de los jugadores de AirSoft
*/
private["_unit","_killer","_uid"];
disableSerialization;
_unit = [_this,0,ObjNull,[ObjNull]] call BIS_fnc_param;
_killer = [_this,1,ObjNull,[ObjNull]] call BIS_fnc_param;

//Set some vars
_unit setVariable["Revive",FALSE,TRUE]; //Set the corpse to a revivable state.
_unit setVariable["name",profileName,TRUE]; //Set my name so they can say my name.
_unit setVariable["restrained",FALSE,TRUE];
_unit setVariable["Escorting",FALSE,TRUE];
_unit setVariable["transporting",FALSE,TRUE]; //Why the fuck do I have this? Is it used?
_unit setVariable["steam64id",(getPlayerUID player),true]; //Set the UID.

//Setup our camera view
life_deathCamera  = "CAMERA" camCreate (getPosATL _unit);
showCinemaBorder TRUE;
life_deathCamera cameraEffect ["Internal","Back"];
createDialog "AirSoftDeathScreen";
life_deathCamera camSetTarget _unit;
life_deathCamera camSetRelPos [0,3.5,4.5];
life_deathCamera camSetFOV .5;
life_deathCamera camSetFocus [50,0];
life_deathCamera camCommit 0;

(findDisplay 7345) displaySetEventHandler ["KeyDown","if((_this select 1) == 1) then {true}"]; //Block the ESC menu

// Telo: Se elimina el equipo del personaje para evitar la caida descontrolada de armas.
RemoveAllWeapons _unit;
{_unit removeMagazine _x;} foreach (magazines _unit);
removeUniform _unit;
removeVest _unit;
removeBackpack _unit;
removeGoggles _unit;
removeHeadGear _unit;
{
    _unit unassignItem _x;
    _unit removeItem _x;
} foreach (assignedItems _unit);

//Create a thread for something?
_unit spawn
{
	private["_maxTime","_RespawnBtn","_Timer"];
	disableSerialization;
	_RespawnBtn = ((findDisplay 7345) displayCtrl 7346);
	_Timer = ((findDisplay 7345) displayCtrl 7347);

	_maxTime = time + 7;
	_RespawnBtn ctrlEnable false;
	waitUntil {_Timer ctrlSetText format[localize "STR_Medic_Respawn",[(_maxTime - time),"MM:SS.MS"] call BIS_fnc_secondsToString];
	round(_maxTime - time) <= 0 OR isNull _this};
	_RespawnBtn ctrlEnable true;
	_Timer ctrlSetText localize "STR_Medic_Respawn_2";
};

//Create a thread to follow with some what precision view of the corpse.
[_unit] spawn
{
	private["_unit"];
	_unit = _this select 0;
	waitUntil {if(speed _unit == 0) exitWith {true}; life_deathCamera camSetTarget _unit; life_deathCamera camSetRelPos [0,3.5,4.5]; life_deathCamera camCommit 0;};
};

// Telo: Hay que enviar el dinero al matador.
if (!isNull _killer) then {
	if (_killer != _unit) then {
		[[1000,profileName],"TON_fnc_airSoftMoneyKill",_killer,false] spawn life_fnc_MP;
	};
};

if (playerSide == civilian) then {
    life_is_alive = false;
};

[] call life_fnc_hudUpdate; //Get our HUD updated.*/

